WHITESPACE = _{ " " }
EMPTY_LINE = _{"" ~ NEWLINE | (" ")* ~ NEWLINE}

COMMENT = _{ "//" ~ (!NEWLINE ~ ANY)* ~ NEWLINE }

ident = @{ (ASCII_ALPHANUMERIC | "_" )+ }

num = @{ ASCII_DIGIT+ }

time_digits = { "s" | "m" | "h" }

node_kind = { "BITCOIND_MINER" | "BITCOIND" | "LND" | "CORELN" | "ECLAIR" }

image_name = @{ ASCII_ALPHANUMERIC+ }
image_version = @{ (ASCII_ALPHANUMERIC | PUNCTUATION)+ }

node_def = { (node_kind ~ ident ~ image_name) | (node_kind ~ ident )  }

node_image = { node_kind ~ "IMAGE" ~ image_name ~ image_version }

node_pair = { node_kind ~ (( ident ~ "PAIR") | ( ident ~ image_name ~ "PAIR")) ~ (ident ~ num | ident) }

skip_conf = { "SKIP_CONF" }
conf = { node_pair | node_image | node_def  }
every = { "EVERY" }
loop = { "LOOP" }
tag = { "TAG" ~ ident }

start  = { ((loop ~ num ~ every ~ num ~ time_digits ) | (loop ~ num) | (loop ~ every ~ num ~ time_digits ))  }
end = { "END" }

loop_content = {
    start ~ NEWLINE ~ ( (ln_node_action | btc_node_action) ~ NEWLINE )* ~ end
}

up = { "UP" }
flag = { "--" }
sub_command = { ( flag ~ ident | num )* }
ln_timeout = { "TIMEOUT" ~ (num)* ~ time_digits }
ln_amount = {"AMT" ~ num }
ln_blocks = {"BLOCKS" ~ num }

ln_node_action_type = { "OPEN_CHANNEL"  | "SEND_LN" | "SEND_HOLD_LN" | "SETTLE_HOLD_LN" | "SEND_ON_CHAIN" | "CLOSE_CHANNEL" | "FORCE_CLOSE_CHANNEL" | "STOP_LN" | "START_LN" | "WAIT" }
ln_node_action = { (image_name ~ ln_node_action_type ~ ln_blocks) | (image_name ~ ln_node_action_type ~ tag) | (image_name ~ ln_node_action_type ~ image_name ~ (ln_amount ~ (tag | ln_timeout ~ tag | sub_command)) | ln_amount ) | (image_name ~ ln_node_action_type ~ ( image_name ~ tag | image_name ~ sub_command | image_name)) | (image_name ~ ln_node_action_type) }

btc_node_action_type = { "MINE_BLOCKS" | "STOP_BTC" | "START_BTC" | "SEND_COINS" }
btc_node_action = { (image_name ~ btc_node_action_type ~ image_name ~ "AMT" ~ (num ~ sub_command | num )) | (image_name ~ btc_node_action_type ~ ( num ~ sub_command | num)) | (image_name ~ btc_node_action_type) }

page = { SOI ~ ( EMPTY_LINE | COMMENT |  ( (EMPTY_LINE | (skip_conf ~ NEWLINE) | (EMPTY_LINE | conf ~ NEWLINE)* ~ (up ~ NEWLINE) )  ~ ( EMPTY_LINE | (loop_content* ~ NEWLINE ) | (ln_node_action ~ NEWLINE ) | (btc_node_action ~ NEWLINE ) )*) ) ~ EOI }

